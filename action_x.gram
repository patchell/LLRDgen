TERMINALS
	"IDENT" = 256,
	"GLOBALS",
	"ROUTINES",
	"MODULE",
	"RETURN",
	"PROC",
	"FUNC",
	"FUNC_IDENT",
	"PROC_IDENT",
	"IF",
	"FOR",
	"WHILE",
	"DO",
	"OD",
	"EXIT",
	"THEN",
	"FI",
	"ELSEIF",
	"ELSE",
	"UNTILL",
	"TO",
	"STEP",
	"ASM",
	"ENDASM",
	"OR",
	"AND",
	"GTEQ",
	"LTEQ",
	"NUMBER",
	"DEFINE",
	"TYPE",
	"RECORDTYPE",
	"BOOL",
	"CHAR",
	"BYTE",
	"CARD",
	"INT",
	"POINTER",
	"ARRAY",
	"BEGIN",
	"END",
	"@",
	".",
	"^",
	"!",
	"&",
	"%",
	"#",
	">",
	"<",
	"=",
	":=",
	"==+",
	"==-",
	"==*",
	"==/",
	"==MOD",
	"==&",
	"==!",
	"==XOR",
	"==%",
	"==LSH",
	"==RSH",
	"[",
	"]",
	",",
	";",
	"+",
	"-",
	"*",
	"/",
	"MOD",
	"LSH",
	"RSH",
	"(",
	")"
END_TERMINALS

GRAMMAR
//--------------------------------
// Program Structure
//--------------------------------
Action65	->Modules;

Modules		-> 'MODULE' Modules_1
			;
Modules_1	-> 'MODULE' Module Modules_1
			-> .
			;

Module		-> SysDecls Routines
			-> Routines
			;

Routines	-> Routine Routines_1;
Routines_1	-> 'END' Routine Routines_1
			-> .
			;
Routine	-> 'PROC' ProcDecl 'BEGIN' ProcBody
		-> 'FUNC' FundType FuncDecl 'BEGIN' ProcBody
		;

ProcBody	-> SysDecls ProcBody_1;
ProcBody_1	-> Statements;

ProcDecl	-> ProcIdent ProcInit '(' ParamList ')';
FuncDecl	-> FuncIdent ProcInit '(' ParamList ')';

ProcIdent	-> 'PROC_IDENT';
FuncIdent	-> 'FUNC_IDENT';

ProcInit -> '=' ArithExpr
		-> .
		;

//------------------------------------------
// Statements
//------------------------------------------
Statements		->Statement Statements_1;
Statements_1	-> ';' Statement Statements_1
				-> 'END' Statement Statements_1
				-> 'FI' Statement Statements_1
				-> 'OD' Statement Statements_1
				-> ')' Statement Statements_1
				-> .
				;

Statement	-> Ident MemContentsType ASSIGNstmt 
			-> ProcIdent '(' IdentList  
			-> 'IF' IfStmt 
			-> 'FOR' ForStmt 
			-> 'WHILE' WhileLoop 
			-> 'DO' DoLoop 
			-> 'EXIT' 
			-> 'RETURN' OptReturnData 
			-> 'BEGIN' Statements
			-> '[' CodeBlock
			;

OptReturnData	-> '(' ArithExpr ')'
				-> .
				;

ASSIGNstmt	->  ':=' ArithExpr
			->  '==+' ArithExpr
			->  '==-' ArithExpr
			->  '==*' ArithExpr
			->  '==/' ArithExpr
			->  '==MOD' ArithExpr
			->  '==&' ArithExpr
			->  '==!' ArithExpr
			->  '==XOR' ArithExpr
			->  '==%' ArithExpr
			->  '==LSH' ArithExpr
			->  '==RSH' ArithExpr
			;

IfStmt		-> CondExpr ThenPart;
ThenPart	-> 'THEN' ThenPart_1;
ThenPart_1	-> Statements ElseIfPart;
ElseIfPart	-> 'ELSEIF' CondExpr ThenPart
			-> 'ELSE' Statements
			-> .
			;

DoLoop	-> Statements UNTILLstmt;
UNTILLstmt	-> 'UNTILL' CondExpr;

WhileLoop	-> CondExpr 'DO' Statements;

FORloop		-> Ident '=' Start 'TO' Finish STEPoption 'DO' Statements;
Start		-> ArithExpr;
Finish		-> ArithExpr;
STEPoption	-> 'STEP' ArithExpr
			-> .
			;

CodeBlock	-> ArithExpr CodeBlock_1;
CodeBlock_1	-> ',' ArithExpr CodeBlock_1
			-> .
			;


//--------------------------------------
// Arithmetic Expressions
//--------------------------------------

ArithExpr	-> LogicalAND LogicalOR_1;
LogicalOR_1	-> 'OR' LogicalAND LogicalOR_1
			-> .
			;

LogicalAND		-> RelOperation LogicalAND_1;
LogicalAND_1	-> 'AND' RelOperation LogicalAND_1
				-> .
				;

RelOperation	-> RelEquals RelOperation_1;
RelOperation_1	-> '>' RelEquals RelOperation_1
				-> '<' RelEquals RelOperation_1
				-> 'GTEQ' RelEquals RelOperation_1
				-> 'LTEQ' RelEquals RelOperation_1
				-> .
				;

RelEquals	-> BitwiseOR RelEquals_1;
RelEquals_1	-> '=' BitwiseOR RelEquals_1
			-> '#' BitwiseOR RelEquals_1
			-> .
			;

BitwiseOR	-> BitwiseAND BitwiseOR_1;
BitwiseOR_1	-> '%' BitwiseAND BitwiseOR_1
			-> .
			;

BitwiseAND		-> BitwiseXOR BitwiseAND_1;
BitwiseAND_1	-> '&' BitwiseXOR BitwiseAND_1
				-> .
				;

BitwiseXOR	-> AddExpr SimpleRelExpr_1;
SimpleRelExpr_1	-> '!' AddExpr SimpleRelExpr_1
				-> .
				;

AddExpr	-> ShifExpr AddExpr_1;
AddExpr_1	-> '+' ShifExpr AddExpr_1
			-> '-' ShifExpr AddExpr_1
			-> .
			;

ShifExpr	-> MultExpr ShiftExpr_1;
ShiftExpr_1	-> 'LSH' MultExpr ShiftExpr_1
			-> 'RSH' MultExpr ShiftExpr_1
			-> .
			;

MultExpr	-> Unary MultExpr_1;
MultExpr_1	-> '*' Unary MultExpr_1
			-> '/' Unary MultExpr_1
			-> 'MOD' Unary MultExpr_1
			-> .
			;

Unary	-> '-' Unary
		-> Factor
		;

Factor	-> BaseCompConst
		-> FUNCcall
		-> '(' ArithExpr ')'
		-> Ident MemContentsType
		;

FUNCcall	-> FuncIdent '(' IdentList ')';

IdentList	-> Ident IdentList_1;
IdentList_1	-> ',' Ident IndentList_1
			-> .
			;
//-------------------------------------------
// Declarations
//-------------------------------------------
FundType	-> 'BOOL' 
			-> 'CHAR' OptModifier
			-> 'BYTE' OptModifier
			-> 'INT' OptModifier
			-> 'CARD' OptModifier
			;

OptModifier	-> 'POINTER'
			-> .
			;
ParamList	-> Param ParamList_1;
ParamList_1	-> ',' Param ParamList_1
			-> .
Param		-> 'RECORDTYPE' OptModifier Ident
			-> 'CHAR' OptModifier Ident
			-> 'BYTE' OptModifier Ident
			-> 'INT' OptModifier Ident
			-> 'CARD' OptModifier Ident
			-> 'BOOL' OptModifier Ident
			;

VarDecls	-> 'RECORDTYPE' Modifier VarList
			-> 'CHAR' Modifier VarList
			-> 'BYTE' Modifier VarList
			-> 'INT' Modifier VarList
			-> 'CARD' Modifier VarList
			-> 'BOOL' Modifier VarList
			;

SysDecls	-> SysDecl SysDecls_1;
SysDecls_1	-> ';' SysDecl SysDecls_1
			-> .
			;

SysDecl		-> 'DEFINE' DefList
			-> 'TYPE' RecIdent
			-> 'RECORDTYPE' Modifier VarList
			-> 'CHAR' Modifier VarList
			-> 'BYTE' Modifier VarList
			-> 'INT' Modifier VarList
			-> 'CARD'D Modifier VarList
			-> 'BOOL' VarList
			-> .
			;

VarList		-> VarName VarList_1;
VarList_1	-> ',' VarName VarList_1
			-> .
			;
VarName		->  Ident OptInit;
OptInit		-> '=' ArithExpr
			-> .
			;

DefList	-> Def DefList_1;
DefList_1	-> ',' Def DefList_1
			-> .
			;

Def		-> Ident '=' Constant;

//--------------------------------------
// TYPE Definition
//--------------------------------------
RecIdent		-> RecField;
RecField		->  '=' '[' RecFieldList ']';
RecFieldList	-> VarDecls RecFieldList_1;
RecFieldList_1	-> ';' VarDecls RecFieldList_1
				-> .
			;


Modifier	-> 'POINTER'
			-> 'ARRAY' ArrayStuff
			-> .
			;

ArrayStuff	-> '(' ArotjExpr ')' OptArrayInit
			-> .
			;
OptArrayInit	-> '=' OptArrayInit_1
				-> .
				;
OptArrayInit_1	-> '[' OptArrayInit_2 ']' ;
OptArrayInit_2	-> ArithExpr OptArrayInit_3 ;
OptArrayInit_3	-> ',' ArithExpr OptArrayInit_3
				-> .
				;

//-------------------------------
// Compiler Constants
//-------------------------------
BaseCompConst	-> 'NUMBER'
				-> '*'
				-> '@' Ident
				;


Ident	-> 'IDENT';

//----------------------------------
//Variable References
//Memory References
//----------------------------------


MemContentsType 	-> '(' ArithExpr ')'	//array ref
					-> '^'	// Pointer Dereference
					-> '.' Ident	//record ref
					-> .
					;

END_GRAMMAR
